dependencies {
    implementation 'com.bugfender.sdk:android:1.0.11'
}

android {
    buildTypes.each {
        it.buildConfigField 'String', 'BUGFENDER_API_KEY', javaLiteralString(getBugfenderApiKey())
    }
}

def getBugfenderApiKey() {
    def localPropsFile = project.rootProject.file('local.properties')
    if (!localPropsFile.exists()) {
        println("file $localPropsFile not found, Bugfender is disabled")
        return null
    }
    Properties properties = new Properties()
    properties.load(localPropsFile.newDataInputStream())
    def key = "bugfender.apiKey"
    def value = properties.getProperty(key)
    if (value == null) {
        println("no $key found in $localPropsFile, Bugfender is disabled")
        return null
    }
    return value
}

static def javaLiteralString(value) {
    if (value == null) {
        return "null"
    } else {
        return "\"$value\""
    }
}